import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'com.gradleup.shadow' version '8.3.6'
	id 'maven-publish'
	id 'eclipse'
	id 'java'
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += ['-source', '' + 21, '-target', '' + 21]
}

compileTestJava.options.encoding = 'UTF-8'
compileJava.options.encoding = 'UTF-8'

repositories {
	mavenCentral()

	// Spigot
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}

	// Bungeecord
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}

	// Skript
	maven {
		url 'https://repo.skriptlang.org/releases'
	}

}

dependencies {

	//Spigot/Bukkit
	implementation (group: 'org.spigotmc', name: 'spigot-api', version: '1.21.5-R0.1-SNAPSHOT')

	//Skript
	implementation (group: 'com.github.SkriptLang', name: 'Skript', version: '2.10.2') {
		transitive = false
	}

	shadow group: 'org.bstats', name: 'bstats-bukkit', version: '3.1.0'

	shadow (group: 'org.json', name: 'json', version: '20250107')

}

processResources {
	filter ReplaceTokens, tokens: ["version": project.property("version")]
	from ('lang') {
			include '**/*'
			into 'lang/'
	}
}

shadowJar {
	configurations = [project.configurations.shadow]
	dependencies {
		include(dependency('org.bstats:bstats-bukkit'))
		include(dependency('org.bstats:bstats-base'))
	}
	relocate 'org.bstats', 'com.skquery.skquery.bstats'
	archiveVersion = version
	archiveFileName = project.name + "-" + version + ".jar"
	minimize()
}
